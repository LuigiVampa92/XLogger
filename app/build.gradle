plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

def appVersionCode = 1
def appVersionName = "0.7"
def minSdkValue = 23
def maxSdkValue = 33

android {
    compileSdk maxSdkValue
    defaultConfig {
        applicationId "com.luigivampa92.xlogger"
        minSdk minSdkValue
        targetSdk maxSdkValue
        versionCode appVersionCode
        versionName appVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "LOG_TAG", "\"XLOGGER\""
    }
    signingConfigs {
        release
        def props = new Properties()
        def propFile = new File('signing.properties')
        if (propFile.canRead()) {
            props.load(new FileInputStream(propFile))
            if (props!=null && props.containsKey('keystore.file') && props.containsKey('keystore.password') && props.containsKey('key.alias') && props.containsKey('key.password')) {
                android.signingConfigs.release.storeFile = file(props['keystore.file'])
                android.signingConfigs.release.storePassword = props['keystore.password']
                android.signingConfigs.release.keyAlias = props['key.alias']
                android.signingConfigs.release.keyPassword = props['key.password']
            }
        } else {
            android.buildTypes.release.signingConfig = null
        }
    }
    buildTypes {
        release {
            buildConfigField "Boolean", "LOGS_ENABLED", "false"
            buildConfigField "int", "LOG_LEVEL", "android.util.Log.INFO"
            zipAlignEnabled true
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            buildConfigField "Boolean", "LOGS_ENABLED", "true"
            buildConfigField "int", "LOG_LEVEL", "android.util.Log.DEBUG"
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled true
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        encoding "UTF-8"
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def separator = "_"
            def projectName = variant.applicationId
            def buildType = variant.buildType.name
            def versionName = variant.versionName
            def datetime = new Date().format('ddMMyyyy_HHmm')
            def apkName = projectName + separator + versionName + separator + buildType + separator + datetime + ".apk"
            output.outputFileName = "${apkName}"
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.core:core-ktx:1.7.0"

    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    implementation "androidx.room:room-runtime:2.4.3"
    implementation "androidx.room:room-rxjava2:2.4.3"
    kapt "androidx.room:room-compiler:2.4.3"

    compileOnly files("libs/XposedBridgeApi-54.jar")

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}